# chapter 2 exercises

## 2.1
```scheme
(define (sign x) (if (= x 0) 0 (/ x (abs x))))

(define (make-rat n d)
  (define (make-rat-sub n d)
    (let ((g (gcd n d)))
      (cons (/ n g) (/ d g))))
  (let ((fsign (* (sign n) (sign d))))
    (set! n (* fsign (abs n)))
    (set! d (abs d))
    (make-rat-sub n d)))
```

## 2.2
```scheme
(define (average x y)
  (/ (+ x y) 2.0))

(define (make-point x y) (cons x y))
(define (x-point p) (car p))
(define (y-point p) (cdr p))
(define (print-point p)
  (newline)
  (display "(")
  (display (x-point p))
  (display ",")
  (display (y-point p))
  (display ")"))

(define (make-segment p1 p2) (cons p1 p2))
(define (start-segment s) (car s))
(define (end-segment s) (cdr s))
(define (print-segment s)
  (newline)
  (display "<")
  (print-point (start-segment s))
  (display ",")
  (print-point (end-segment s))
  (display ">"))
(define (midpoint-segment s)
  (make-point
    (average (x-point (start-segment s)) (x-point (end-segment s)))
    (average (y-point (start-segment s)) (y-point (end-segment s)))))

(define mys (make-segment (make-point 12 13) (make-point 13 15)))
(print-point (midpoint-segment mys))
```

# 2.3
```scheme
(define (y-distance p1 p2)
  (- (y-point p1) (y-point p2)))
(define (x-distance p1 p2)
  (- (x-point p1) (x-point p2)))
(define (distance p1 p2)
  (sqrt (+
    (square (x-distance p1 p2))
    (square (y-distance p1 p2)))))

(define (make-rectangle height breadth)
  (cons height breadth))
(define (height-rectangle r) (car r))
(define (breadth-rectangle r) (cdr r))
(define (area-rectangle r)
  (* (height-rectangle r) (breadth-rectangle r)))
(define (perimeter-rectangle r)
  (* 2 (+ (height-rectangle r) (breadth-rectangle r))))


;; alternative representation
;; given 4 points, make-rectangle-points will call make-rectangle with calculated height and breadth
;; assumes <p1, p2> and <p3 and p4> create distinct sides of the rectangle
;; works as long as the 'rectangle' data object is only used to find perimeter and area
(define (make-rectangle-points p1 p2 p3 p4)
  (let ((l1 (distance p1 p2)) (l2 (distance p3 p4)))
  (make-rectangle l1 l2)))

(define rectangle1
  (make-rectangle-points
    (make-point 0 0)
    (make-point 1 0)
    (make-point 1 1)
    (make-point 0 1)))

(area-rectangle rectangle1)
(perimeter-rectangle rectangle1)
```

## 2.4
```scheme
(define (cdr z)
  (z (lambda (p q) q)))
```

## 2.5
```scheme
(define (divisibility a b)
  (define (iter x)
    (if (or (> (remainder x b) 0) (= x 0))
      0
      (+ 1 (iter (/ x b)))))
  (iter a))

(divisibility 108 3)

(define (cons x y)
  (* (expt 2 x) (expt 3 y)))

(define (car p)
  (divisibility p 2))

(define (cdr p)
  (divisibility p 3))


(define mypair (cons 14 45))
(car mypair)
(cdr mypair)
```

## 2.6
```scheme
(define one (lambda (f) (lambda (x) (f x))))
(define two (lambda (f) (lambda (x) (f (f x)))))

(define (add-numbers a)
  (lambda (b)
    (lambda (f)
      (lambda (x)
       ((a f) ((b f) x))))))

((zero add-1) 0)
((((one add-1) zero) add-1) 0)
((two add-1) 0)
(((one add-1) zero) 0)

;; todo: figure out how to get simple numbers back from the add-numbers procedure
```

## 2.7
```scheme
(define (make-interval a b) (cons a b))
(define (lower-bound x) (car x))
(define (upper-bound x) (cdr x))
```


## 2.8
```scheme
(define (sub-interval x y)
  (make-interval
    (- (lower-bound x) (upper-bound y))
    (- (upper-bound x) (lower-bound y))))
```

## 2.9
```scheme
(define (width x) (/ (- (upper-bound x) (lower-bound x)) 2))


;(width (add-interval x y))

;(width (make-interval
;  (+ (lower-bound x) (lower-bound y))
;  (+ (upper-bound x) (upper-bound y))))


;(/ (- (+ (upper-bound x) (upper-bound y)) (+ (lower-bound x) (lower-bound y))) 2)

;(+ (width x) (width y))

;^are equivalent





(define i1 (make-interval 1.6 2.4))
;Value: i1

(define i2 (make-interval 1.4 2.4))
;Value: i2

(width i1)
;Value: .3999999999999999

(width i2)
;Value: .5

(width (mul-interval i1 i2))
;Value: 1.76
```

## 2.10
```scheme
(define (div-interval x y)
  (if (= (width y) 0)
    (error "width of interval y is 0")
    (mul-interval
      x
      (make-interval
        (/ 1.0 (upper-bound y))
        (/ 1.0 (lower-bound y))))))
```

## 2.11
```scheme
(define (mul-interval x y)
  (let
    ((l1 (lower-bound x))
      (u1 (upper-bound x))
      (l2 (lower-bound y))
      (u2 (upper-bound y)))
    (if (> l1 0)
      (if (> l2 0)
        (make-interval (* l1 l2) (* u1 u2)) ; l1 > 0, l2 > 0, u1 > 0, u2 > 0
        (if (> u2 0)
          (make-interval (* u1 l2) (* u1 u2))  ; l1 > 0, u1 > 0, l2 < 0, u2 > 0
          (make-interval (* u1 l2) (* l1 u2)))) ; l1 > 0, u1 > 0, l2 < 0, u2 < 0
      (if (> l2 0)
        (if (> u1 0)
          (make-interval (* l1 u2) (* u1 u2))  ; l1 < 0, u1 > 0, l2 > 0, u2 > 0
          (make-interval (* l1 u2) (* u1 l2))) ; l1 < 0, u1 < 0, l2 > 0, u2 > 0
        (if (> u1 0)
          (if (> u2 0)
            (make-interval (min (* u1 l2) (* l1 u2)) (max (* l1 l2) (* u1 u2)))  ; l1 < 0, u1 > 0, l2 < 0, u2 > 0
            (make-interval (* u1 l2) (* l1 l2))) ; l1 < 0, u1 > 0, l2 < 0, u2 < 0
          (if (> u2 0)
            (make-interval (* l1 u2) (* l1 l2))  ; l1 < 0, u1 < 0, l2 < 0, u2 > 0
            (make-interval (* u1 u2) (* l1 l2)))))))); l1 < 0, u1 < 0, l2 < 0, u2 < 0
```

## 2.12
```scheme
(define (make-center-percent c p)
  (make-center-width c (* p c)))

(define (percent i)
  (/ (width i) (center i)))
```


## 2.13
```scheme
(define (mul-interval-center-percent x y)
  (let
    ((c1 (center x))
      (p1 (percent x))
      (c2 (center y))
      (p2 (percent y)))
    (make-center-percent (* c1 c2) (/ (+ p1 p2) (+ 1 (* p1 p2))))))

; if p1 and p2 << 1, p1*p2 may be ignored and the product's error can be approximated by the sum of the two factors' percentage error

(define (mul-interval-center-percent x y)
  (make-center-percent
    (* (center x) (center y))
    (+ (percent x) (percent y))))
```

## 2.14
```scheme
1 ]=> (define a (make-center-percent 10 0.003))
;Value: a

1 ]=> (lower-bound a)
;Value: 9.97

1 ]=> (upper-bound a)
;Value: 10.03

1 ]=> (define div (div-interval a a))
;Value: div

1 ]=> (lower-bound div)
;Value: .9940179461615156

1 ]=> (upper-bound div)
;Value: 1.0060180541624872

1 ]=> (center div)
;Value: 1.0000180001620014

1 ]=> (percent div)
;Value: 5.999946000485802e-3

; whatever values 'a' takes in the interval (9.97, 10.03), a divided by itself must return 1
; however the program is not aware of (9.97, 10.03) repeated being the same interval itself, or another interval with the same range of values
```

## 2.15
```scheme
; yes, because par2 avoids the problem as described in 2.14
```


## 2.16
```scheme
; not unless we devise a way to keep track of the variables that are used repeatedly in an expression
; (a/a) must return '1', but (a/b) must not return '1' even if the bounds of both intervals are the same
```


## 2.17
```scheme
(define (last-pair some-list)
  (define (last-pair-iter a)
    (if (null? (cdr a))
      a
      (last-pair-iter (cdr a))))
  (last-pair-iter some-list))
```

## 2.18
```scheme
(define (reverse some-list)
  (define (reverse-iter remaining ans)
    (if (null? remaining)
      ans
      (reverse-iter (cdr remaining) (cons (car remaining) ans))))
  (reverse-iter some-list '()))
```


## 2.19
```scheme
(define (no-more? list-of-denominations) (null? list-of-denominations))
(define (except-first-denomination list-of-denominations) (cdr list-of-denominations))
(define (first-denomination list-of-denominations) (car list-of-denominations))

no, because sooner or later all possible combinations of splitting change will explored by the recursion tree
```

## 2.20
```scheme
(define (same-parity . x)
  (define (collect-parity-iter p curr ans)
    (if (null? curr)
      (reverse ans) ;todo - get rid of the reverse thing
      (if (= p (mod2 (car curr)))
        (collect-parity-iter p (cdr curr) (cons (car curr) ans))
        (collect-parity-iter p (cdr curr) ans))))
  (let ((parity (mod2 (first x))))
    (collect-parity-iter parity x '())))
```


## 2.21
```scheme
(define (square-list items)
  (if (null? items)
    '()
    (cons
      (square (car items))
      (square-list (cdr items)))))

(define (square-list2 items)
  (map square items))
```


## 2.22
```scheme
(define (square-list items)
  (define (iter things answer)
    (if (null? things)
      answer
      (iter
        (cdr things)
        (cons
          (square (car things))
          answer))))
  (iter items '()))

; we add squared numbers to the start of this list inside the 'iter' subroutine
; we square numbers iteratively starting from the first to last
; therefore, the square of the last number in the original list is the first entry in the answer (and so on)

(define (square-list items)
  (define (iter things answer)
    (if (null? things)
        answer
        (iter
          (cdr things)
          (cons
            answer
            (square (car things))))))
  (iter items '()))

; here, squared numbers are added as the second element in a pair
; the first element is the answer so far
;Value: ((((((() . 1) . 4) . 1156) . 16) . 25) . 25)
```


## 2.23
```scheme
(define (for-each f items)
  ;(newline) (display "for-each invoked: ") (display items)
  (if (null? items)
    #t
    ((for-each f (cdr items)) (f (car items)))))


(for-each
  (lambda (x) (newline) (display x))
  (list 57 321 88))

; super confusing - what is the order of evaluation?
; ((for-each f (cdr items)) (f (car items)))
; it seems like the recursive call should be executed first, but the function 'f' is applied to '(car items)' before the recursion takes place
```

## 2.24
```scheme
1 ]=> (list 1 (list 2 (list 3 4)))
;Value: (1 (2 (3 4)))

;; as a tree:
;;    . (1 (2 (3 4)))
;;   / \
;; 1    \
;;       . (2 (3 4))
;;      / \
;;     2   \
;;          . (3 4)
;;         / \
;;        3   4


;; as box and pointer notation: i drew it on paper dont believe me fuck you
```

## 2.25
```scheme
1 ]=> (car (cdr (car (cdr (cdr (list 1 3 (list 5 7) 9))))))
;Value: 7

1 ]=> (car (car (list (list 7))))
;Value: 7

1 ]=> (car (cdr (car (cdr (car (cdr (car (cdr (car (cdr (car (cdr (list 1 (list 2 (list 3 (list 4 (list 5 (list 6 7))))))))))))))))))
;Value: 7
```

## 2.26
```scheme
1 ]=> (append x y)
;Value: (1 2 3 4 5 6)
;; append creates a new list

1 ]=> (cons x y)
;Value: ((1 2 3) 4 5 6)
;; cons creates a pair as expected, with both elements of the pair being lists

1 ]=> (list x y)
;Value: ((1 2 3) (4 5 6))
;; list creates a list of two lists
```


## 2.27
```scheme
(define (reverse some-list)
  (define (reverse-iter remaining ans)
    (cond
      ((null? remaining) ans)
      ((not (pair? remaining)) remaining)
      (else (reverse-iter
        (cdr remaining)
        (cons (reverse (car remaining)) ans)))))
  (reverse-iter some-list '()))
```

## 2.28
```scheme
(define (fringe tree)
  (cond
    ((null? tree) '())
    ((not (pair? tree)) (list tree))
    (else (append (fringe (car tree)) (fringe (cdr tree))))))
```


## 2.29
```scheme
(define (make-mobile left right) (list left right))
(define (left-branch mobile) (car mobile))
(define (right-branch mobile) (car (cdr mobile)))

(define (make-branch length structure) (list length structure))
(define (branch-length branch) (car branch))
(define (branch-structure branch) (car (cdr branch)))



(define (total-weight mobile)
  (define (total-weight-iter submobile)
    (cond
      ((null? submobile) 0)
      ((not (pair? submobile)) submobile)
      (else (+
        (total-weight-iter (branch-structure (left-branch submobile)))
        (total-weight-iter (branch-structure (right-branch submobile)))))))
  (total-weight-iter mobile))



(define (balanced-mobile? mobile)
  (newline) (display "balanced-mobile called with: ") (display mobile)
  (define (torque branch)
    (* (branch-length branch) (total-weight (branch-structure branch))))
  (cond
    ((null? mobile) #t)
    ((not (pair? mobile)) #t)
    (else (and
      (= (torque (left-branch mobile)) (torque (right-branch mobile)))
      (balanced-mobile? (branch-structure (left-branch mobile)))
      (balanced-mobile? (branch-structure (right-branch mobile)))))))



; only changes in selectors - use (cdr x) instead (car (cdr x))
(define (make-mobile left right) (cons left right)) 
(define (left-branch mobile) (car mobile))
(define (right-branch mobile) (cdr mobile))

(define (make-branch length structure) (cons length structure))
(define (branch-length branch) (car branch))
(define (branch-structure branch) (cdr branch))
```


## 2.30
```scheme
(define (square-tree tree)
  (cond
    ((null? tree) '())
    ((not (pair? tree)) (* tree tree))
    (else (cons (square-tree (car tree)) (square-tree (cdr tree))))))

(define (square-tree tree)
  (map (lambda (subtree)
    (if (pair? subtree) (square-tree subtree) (* subtree subtree)))
    tree))
```


## 2.31
```scheme
(define (tree-map f tree)
  (map (lambda (subtree)
    (if (pair? subtree) (tree-map f subtree) (f subtree)))
    tree))

(define (square-tree tree) (tree-map square tree))
```

## 2.32
```scheme
(define (subsets s)
  (if (null? s)
    (list '())
    (let ((rest (subsets (cdr s))))
      (append rest (map (lambda (x) (cons (car s) x)) rest)))))

; all subsets of an empty list are: (())
; all subsets of a set can be created by excluding or including the first element in every subset of the remaining elements
; inclusion happens in the lambda
; exlusion happens when 'rest' is appended to the answer
```


## 2.33
```scheme
(define (accumulate op initial sequence)
  (if (null? sequence)
    initial
    (op
      (car sequence)
      (accumulate op initial (cdr sequence)))))


(define (append x y)
  (accumulate cons y x))

(define (map p sequence)
(accumulate
  (lambda (current rest) (cons (p current) rest))
  '()
  sequence))

(define (length sequence)
  (accumulate (lambda (current rest) (+ 1 rest)) 0 sequence))
```


## 2.34
```scheme
(define (horner-eval x coeffecient-sequence)
  (accumulate
    (lambda (this-coeff higher-terms) (+ this-coeff (* x higher-terms)))
    0
    coeffecient-sequence))
```


## 2.35
```scheme
(define (count-leaves-2 x)
  (accumulate
    +
    0
    (map
      (lambda (node)
        (cond
          ((null? node) 0)
          ((not (pair? node)) 1)
          (else (count-leaves-2 node))))
      x)))
```


## 2.36
```scheme
(define (accumulate-n op init seqs)
  (if (null? (car seqs))
    '()
    (cons
      (accumulate op init (map (lambda (x) (car x)) seqs))
      (accumulate-n op init (map (lambda (x) (cdr x)) seqs)))))
```


## 2.37
```scheme
(define (dot-product v w)
  (accumulate + 0 (map * v w)))

(define (matrix-*-vector m v)
  (map (lambda (row) (dot-product row v)) m))

(define (matrix-*-matrix m n)
  (let ((cols (transpose n)))
    (map (lambda (m-row) (matrix-*-vector cols m-row)) m)))
```


## 2.38
```scheme
(fold-right / 1 (list 1 2 3))
;Value: 3/2

(fold-left / 1 (list 1 2 3))
;Value: 1/6

(fold-right list '() (list 1 2 3))
;Value: (1 (2 (3 ())))

(fold-left list '() (list 1 2 3))
;Value: (((() 1) 2) 3)


; op must be associative and commutative for fold-left and fold-right to produce the same values
; fold-right expands to: (op 1 (op 2 (op 3 (op 4 init))))
; fold-left expands to: (op (op (op (op 1 init) 2) 3) 4))))
```


## 2.39
```scheme
(define (reverse-r sequence)
  (fold-right (lambda (current-element rest-result) (append rest-result (list current-element))) '() sequence))

(define (reverse-l sequence)
  (fold-left (lambda (current-element rest-result) (cons rest-result current-element)) '() sequence))
```


## 2.40
```scheme
(define (unique-pairs n)
  (flatmap
    (lambda (i)
        (map
          (lambda (j) (list i j))
          (enumerate-interval 1 (- i 1))))
    (enumerate-interval 1 n)))

(define (prime-sum-pairs n)
  (map make-pair-sum (filter prime-sum? (unique-pairs n))))
```


## 2.41
```scheme
(define (sum-to? s) (lambda (triplet) (= s (+ (car triplet) (cadr triplet) (caddr triplet)))))

(define (unique-triplets n)
  (flatmap
    (lambda (i)
      (map
        (lambda (pair) (append (list i) pair))
        (unique-pairs (- i 1))))
    (enumerate-interval 1 n)))

(define (find-sum-triplets sum n)
  (filter (sum-to? sum) (unique-triplets n)))
```


## 2.42
```scheme
(define empty-board '())

(define (adjoin-position new-queen-pos k other-queens)
  (cons new-queen-pos other-queens))

; positions are lists of size 'k', where each element lies between 1 and 'board-size'
; only the new queen needs to be verified to be safe
(define (safe? k positions)
  (let ((new-queen (car positions)))
    (define (safe-iter col-distance remaining-queens)
      (if (null? remaining-queens)
        #t
        (cond
          ((= (car remaining-queens) new-queen) #f) ;same row
          ((= col-distance (abs (- new-queen (car remaining-queens)))) #f)  ;same diagonal
          (else (safe-iter (+ col-distance 1) (cdr remaining-queens)))))) ;check other queens
    (safe-iter 1 (cdr positions))))
```


## 2.43
```scheme
(flatmap
  (lambda (new-row)
    (map
      (lambda (rest-of-queens)
        (adjoin-position new-row k rest-of-queens))
      (queen-cols (- k 1))))
  (enumerate-interval 1 board-size))


;the inner map now calls queen-cols recursively, for every new-row
;this is unoptimal, since the values of (queen-cols (-k 1)) is constant regardless of the value held by new-row

;time complexity of the correct implementation can be expressed as O()????
;queen-cols will filter out unsafe positions, so a simple factorial is not correct
;todo
```


## 2.44
```scheme
(define (up-split painter n)
  (if (= n 0)
    painter
    (let ((smaller (up-split painter (- n 1))))
      (below painter (beside smaller smaller)))))
```


## 2.45
```scheme
(define (split t1 t2)
  (lambda (painter n)
    (if (= n 0)
      painter
      (let ((smaller ((split t1 t2) painter (- n 1))))
        (t1 painter (t2 smaller smaller))))))
```


## 2.46
```scheme
(define make-vect cons)
(define xcor-vect car)
(define ycor-vect cdr)

(define (add-vect v1 v2)
  (make-vect
    (+ (xcor-vect v1) (xcor-vect v2))
    (+ (ycor-vect v1) (ycor-vect v2))))

(define (sub-vect v1 v2)
  (make-vect
    (- (xcor-vect v1) (xcor-vect v2))
    (- (ycor-vect v1) (ycor-vect v2))))

(define (scale-vect s v)
  (make-vect
    (* s (xcor-vect v))
    (* s (ycor-vect v))))
```


## 2.47
```scheme
(define (origin-frame-1 frame) (first frame))
(define (edge1-frame-1 frame) (second frame))
(define (edge2-frame-1 frame) (third frame))


(define (origin-frame-2 frame) (car frame))
(define (edge1-frame-2 frame) (cadr frame))
(define (edge2-frame-2 frame) (cddr frame))
```


## 2.48
```scheme
(define make-segment cons)
(define start-segment car)
(define end-segment cdr)
```


## 2.49
```scheme
(define (outline->painter)
  (segments->painter
    (list 
      (make-segment (make-vect 0 0) (make-vect 0 1))
      (make-segment (make-vect 0 0) (make-vect 1 0))
      (make-segment (make-vect 1 0) (make-vect 1 1))
      (make-segment (make-vect 0 1) (make-vect 1 1)))))


(define (X->painter)
  (segments->painter
    (list
      (make-segment (make-vect 0 0) (make-vect 1 1))
      (make-segment (make-vect 0 1) (make-vect 1 0)))))

(define (diamond->painter)
  (segments->painter
    (list
      (make-segment (make-vect 0.5 0) (make-vect 1 0.5))
      (make-segment (make-vect 1 0.5) (make-vect 0.5 1))
      (make-segment (make-vect 0.5 1) (make-vect 0 0.5))
      (make-segment (make-vect 0 0.5) (make-vect 0.5 0)))))


(define (diamond->painter)
  (segments->painter
    (list
      (segment (vect 0.5 0) (vect 1 0.5))
      (segment (vect 1 0.5) (vect 0.5 1))
      (segment (vect 0.5 1) (vect 0 0.5))
      (segment (vect 0 0.5) (vect 0.5 0)))))


(define (wave->painter)
  (segments->painter
    (let
      ((a (vect 0.2 0))
        (b (vect 0.4 0.6))
        (c (vect 0.3 0.5))
        (d (vect 0 0.7))
        (e (vect 0 0.8))
        (f (vect 0.3 0.6))
        (g (vect 0.4 0.7))
        (h (vect 0.3 0.85))
        (i (vect 0.4 1))
        (j (vect 0.6 1))
        (k (vect 0.7 0.85))
        (l (vect 0.6 0.7))
        (m (vect 0.8 0.8))
        (n (vect 1 0.6))
        (o (vect 1 0.5))
        (missed (vect 0.8 0.7))
        (p (vect 0.6 0.6))
        (q (vect 0.8 0))
        (r (vect 0.7 0))
        (s (vect 0.5 0.4))
        (t (vect 0.3 0)))
      (list
        (segment a b)
        (segment b c)
        (segment c d)
        (segment e f)
        (segment f g)
        (segment g h)
        (segment h i)
        (segment j k)
        (segment k l)
        (segment l m)
        (segment m n)
        (segment o missed)
        (segment missed p)
        (segment p q)
        (segment r s)
        (segment s t)))))
```


## 2.50
```scheme
(define (flip-horiz painter)
  (transform-painter
    painter
    (make-vect 1 0)
    (make-vect 0 0)
    (make-vect 1 1)))

(define (rotate-180-cc painter)
  (transform-painter
    painter
    (make-vect 1 1)
    (make-vect 0 1)
    (make-vect 1 0)))

(define (rotate-270-cc painter)
  (transform-painter
    painter
    (make-vect 0 1)
    (make-vect 0 0)
    (make-vect 1 1)))
```


## 2.51
```scheme
(define (below-1 painter1 painter2)
  (let ((split-point (make-vect 0 0.5)))
    (let
      ((paint-top
        (transform-painter
          painter2
          (make-vect 0 0)
          (make-vect 1 0)
          split-point))
      (paint-bottom
        (transform-painter
          painter1
          split-point
          (make-vect 1 0.5)
          (make-vect 0 1))))
      (lambda (frame) (paint-top frame) (paint-bottom frame)))))

(define (below-2 painter1 painter2)
  (rotate-270-cc (beside (rotate-90-cc painter1) (rotate-90-cc painter2))))
```


## 2.52
```scheme
(define wave->painter
  (segments->painter
    (let
      ((a (vect 0.2 0))
        (b (vect 0.4 0.6))
        (c (vect 0.3 0.5))
        (d (vect 0 0.7))
        (e (vect 0 0.8))
        (f (vect 0.3 0.6))
        (g (vect 0.4 0.7))
        (h (vect 0.3 0.85))
        (i (vect 0.4 1))
        (j (vect 0.6 1))
        (k (vect 0.7 0.85))
        (l (vect 0.6 0.7))
        (m (vect 0.8 0.8))
        (n (vect 1 0.6))
        (o (vect 1 0.5))
        (missed (vect 0.8 0.7))
        (p (vect 0.6 0.6))
        (q (vect 0.8 0))
        (r (vect 0.7 0))
        (s (vect 0.5 0.4))
        (t (vect 0.3 0))
        (le1 (vect 0.415 0.9))
        (le2 (vect 0.425 0.9))
        (re1 (vect 0.585 0.9))
        (re2 (vect 0.595 0.9)))
      (list
        (segment a b)
        (segment b c)
        (segment c d)
        (segment e f)
        (segment f g)
        (segment g h)
        (segment h i)
        (segment j k)
        (segment k l)
        (segment l m)
        (segment m n)
        (segment o missed)
        (segment missed p)
        (segment p q)
        (segment r s)
        (segment s t)
        (segment le1 le2)
        (segment re1 re2)))))
; ^ added eyes to the wave->painter

(define (corner-split painter n)
  (if (= n 0)
      painter
      (let ((up (up-split painter (- n 1)))
            (right (right-split painter (- n 1))))
        (let ((top-left (beside right up))
              (bottom-right (below up right)) (corner (corner-split painter (- n 1))))
          (beside (below painter top-left)
                  (below bottom-right corner))))))
; ^ corner-split is way more chaotic this way


(define (square-limit painter n)
  (let ((quarter (corner-split painter n)))
    (let ((half (beside (flip-horiz quarter) quarter)))
      (below (flip-horiz half) half))))
; ^ flip-horiz instead of flip-vert the halves
```


## 2.53
```scheme
(a b c)
((george))
((y1 y2))
(y1 y2)
false
false
(red shoes blue socks)
```


## 2.54
```scheme
(define (equal? seq1 seq2)
  (cond
    ((and (null? seq1) (null? seq2)) #t)
    ((or (null? seq1) (null? seq2)) #f)
    ((eq? (car seq1) (car seq2)) (equal? (cdr seq1) (cdr seq2)))
    (else #f)))
```


## 2.55
```scheme
;the statements
''abracadabra
(quote (quote (abracadabra)))
;are equivalent

(car (quote (quote (abracadabra))))
;will simply return quote
```


## 2.56
```scheme
(define (make-exponentiation base exponent)
  (cond
    ((and (number? exponent) (= exponent 0)) 1)
    ((and (number? exponent)(= exponent 1)) base)
    ((and (number? base )(= base 0)) 0);unless exponent = 0
    (else (list '** base exponent))))

(define (exponentiation? expression)
  (and (pair? expression) (eq? '** (car expression)) (number? (exponent expression))))

(define (base e) (cadr e))

(define (exponent e) (caddr e))

(define (deriv exp var)
  (cond
    ((number? exp) 0)
    ((variable? exp)
      (if (same-variable? exp var) 1 0))
    ((sum? exp)
      (make-sum
        (deriv (addend exp) var)
        (deriv (augend exp) var)))
    ((product? exp)
      (make-sum
        (make-product (multiplier exp) (deriv (multiplicand exp) var))
        (make-product (deriv (multiplier exp) var) (multiplicand exp))))
    ((exponentiation? exp)
      (make-product
        (exponent exp)
        (make-product
          (make-exponentiation (base exp) (- (exponent exp) 1))
          (deriv (base exp) var))))
    (else (error "unknown expression type: DERIV" exp))))
```


## 2.57
```scheme
(define (make-sum . parts)
  (let
    ((num-sum (accumulate + 0 (filter number? parts)))
      (others (filter (lambda (x) (not (number? x))) parts)))
    (cond
      ((and (null? others) (= num-sum 0)) 0)
      ((= 0 num-sum) (cons '+ others))
      ((null? others) num-sum)
      (else (newline) (append (cons '+ others) (list num-sum))))))

(define (sum? x) (and (pair? x) (eq? (car x) '+)))

(define (addend s) (cadr s))

(define (augend s)
  (if (pair? s)
    (if (= (length s) 2)
      0
      (if (= (length s) 3)
        (caddr s)
        (cons '+ (cddr s))))
    s))
  


(define (make-product . parts)
  (let
    ((num-product (accumulate * 1 (filter number? parts)))
      (others (filter (lambda (x) (not (number? x))) parts)))
    (cond
      ((= num-product 0) 0)
      ((= 1 num-product) (cons '* others))
      ((null? others) num-product)
      (else (append (cons '* others) (list num-product))))))

(define (product? x) (and (pair? x) (eq? (car x) '*)))

(define (multiplier p) (cadr p))

(define (multiplicand p)
  (if (pair? p)
    (if (= (length p) 2)
      1
      (if (= (length p) 3)
        (caddr p)
        (cons '* (cddr p))))
    p))
```
