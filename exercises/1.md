# chapter 1 exercises

## 1.1

```scheme
10
12
8
3
6
a
b
19
#f
4
16
6
16
```

## 1.2

```scheme
(/
  (+ 5 (+ 4 (- 2 (- 3 (+ 6 (/ 4 5))))))
  (* 3 (- 6 2) (- 2 7)))
```

## 1.3

```scheme
(define (sqr x) (* x x))

(define (sum-of-sqrs x y) (+ (sqr x) (sqr y)))

(define (sos-larger-2 x y z)
    (cond ((and (< x y) (< x z)) (sum-of-sqrs y z))
            ((and (< y z) (< y x)) (sum-of-sqrs x z))
            (else (sum-of-sqrs x y))))
```


  (lambda (x) x)

## 1.4

```scheme
(define (a-plus-abs-b a b)
    ((if (> b 0) + -) a b))

#|
required operation is: (a + abs(b))
this is equivalent to:
if (b > 0), a + b
else a - b

the given implementation decides whether to use the + or the - operator based on whether b is greater than 0
|#
```

## 1.5

```scheme
(define (p) (p))
(define (test x y)
    (if (= x 0) 0 y))

#|
applicative-order-evaluation (evaluate, use):
    will result in an inf loop, since the self referencing procedure will attempt to evaluate itself

normal-order-evaluation (subsitute, then reduce):
    will return 0 since the predicate of the conditional will be evaluated
|#
```

## 1.6

new-if, being a compound procedure and not a special form, is computed in 2 steps

1. the formal parameters are evaluated as operands
2. the operation, in this case the compound procedure is applied on the operands

this implies that the else-clause passed to this procedure will have to be computed before it is dealt with inside the body of the compound procedure new-if

the special form if allows us to compute the predicate while avoiding the computation of the clauses

## 1.7

attempting to find the square root of a number smaller than the set tolerance threshold will clearly fail

a large number like 99118991284657502 with a tolerance threshold of 0.001 fails, due to the square of guesses not being precise enough for the method to converge

```scheme
(define (good-enough? x guess)
    (define fraction-tolerance 0.001)
    (define next-guess (improve-guess x guess))
    (define change (abs (- next-guess guess)))
    (< (/ change guess) fraction-tolerance))
```

this new definition works decently well for small numbers, however with large numbers the margin of error is similar to the old definition

this is because the calculation of the square of a large guess is not precise

## 1.8

```scheme
(define (improve-guess x guess) (/ (+ (* 2 guess) (/ x (sq guess))) 3))
```

everything else is identical to our impl for finding square roots

## 1.9

```scheme
(define (+ a b)
    (if (= a 0) b (inc (+ (dec a) b))))

#|
(+ 4 5)
(inc (+ 3 5))
(inc (inc (+ 2 5)))
(inc (inc (inc (+ 1 5))))
(inc (inc (inc (inc (+ 0 5)))))
(inc (inc (inc (inc (5)))))
(inc (inc (inc 6)))
(inc (inc 7))
(inc 8)
9
|#

(define (+ a b)
    (if (= a 0) b (+ (dec a) (inc b))))

#|
(+ 4 5)
(+ 3 6)
(+ 2 7)
(+ 1 8)
(+ 0 9)
9
|#

#|
the first process is recursive in nature since the stack stores the number of times inc must be applied to the result of the + procedure

the second process is iterative since the state is stored completely in the formal parameters
|#
```

## 1.10

```scheme
(define (A x y)
    (cond ((= y 0) 0)
        ((= x 0) (* 2 y))
        ((= y 1) 2)
        (else (A (- x 1) (A x (- y 1))))))

#|
(A 1 10)
(A 0 (A 1 9))
(A 0 (A 0 (A 1 8)))
....
(A 0 (A 0 (A 0 (.... (A 0 1)))))
(A 0 (A 0 (A 0 (...... 2))))
2^10 = 1024
|#

#|
(A 2 4)
(A 1 (A 2 3))
(A 1 (A 1 (A 2 2)))
(A 1 (A 1 (A 1 (A 2 1))))
(A 1 (A 1 (A 1 2)))
(A 1 (A 1 (A 0 (A 1 1))))
(A 1 (A 1 (A 0 2)))
(A 1 (A 1 4))
(A 1 16)
2^16 = 65536
|#


#|
(A 3 3)
(A 2 (A 3 2))
(A 2 (A 2 (A 3 1)))
(A 2 (A 2 2))
(A 2 (A 1 (A 2 1))
(A 2 (A 1 2))
(A 2 (A 0 (A 1 1))
(A 2 (A 0 2))
(A 2 4)
already computed, 65536
|#

#|
(define (f n) (A 0 n)) = 2 * n

(define (g n) (A 1 n)) = 2 ^ n

(define (h n) (A 2 n)) = (A 1 (A 2 (-1+ n)))
                        = 2 ^ (h (n - 1))
```

## 1.11
```scheme
(define (compute-f x)
    (if (< x 3)
        x
        (compute-f-iter 2 1 0 (- x 2))
    )
)


(define (compute-f-iter p1 p2 p3 x)
    (if (= x 0)
        p1
        (compute-f-iter (compute-next p1 p2 p3) p1 p2 (- x 1))
    )
)

(define (compute-next p1 p2 p3)
    (+ p1 (+ (* 2 p2) (* 3 p3)))
)
```

## 1.12
```scheme
(define (ptr-element r c)
    (cond
        ((or (= c 0) (= r c)) 1)
        ((or (> c r) (or (< r 0) (< c 0))) -1)
        (else (+ (ptr-element (- r 1) (- c 1)) (ptr-element (- r 1) c)))
    )
)
```

## 1.13
```
prove fib(n) = closest integer to (phi^n / sqrt(5))
phi = (1 + sqrt(5)) / 2
psi = (1 - sqrt(5)) / 2

"rt5" is "sqrt(5)"

hint: using induction, prove fib(n) = (phi^n - psi^n) / rt5

fib(0) = (phi^n - psi^n) / rt5 = 0
holds for n = 0

fib(n + 1) = fib(n) + fib(n - 1) by definition
assuming fib(n) = (phi^n - psi^n) / rt5 is true,

fib(n + 1) = ((phi^n - psi^n) / rt5) + ((phi^(n-1) - psi^(n-1)) / rt5)
fib(n + 1) = (phi^(n-1) * (1 + phi) - psi^(n-1) * (1 + psi)) / rt5

note that
    1 + phi = phi^2,
    1 + psi = psi^2

fib(n + 1) = (phi^(n+1) - psi^(n+1)) / rt5

therefore, fib(n) = (phi^n - psi^n) / rt5
psi = (1 - rt5) / 2 = approx. -0.6
phi = (1 + rt5) / 2 = approx. 1.6

abs(psi) = 0.4 and psi^n -> 0 with increasing n
(fib(n) - phi^n / rt5) = psi^n / rt5 lies between -0.4 and +0.1

therefore, fib(n) is the closest integer to phi^n / rt5
```

## 1.14
```

```


## 1.15
```
a. 5 times
b. every call where x > 0.1, will trigger (sine (/ x 3))
    therefore, time and space will scale by log(x)
```


## 1.16
```scheme
(define (remainder dividend divisor)
  (let ((qoutient (floor (/ dividend divisor))))
  (- dividend (* divisor qoutient))))

(define (sq x) (* x x))

(define (even n) (= (remainder n 2) 0))

(define (fast-exp-recursive base power)
  (if (= power 0)
    1
    (if (even power)
      (fast-exp-recursive (sq base) (/ power 2))
      (* base (fast-exp-recursive (sq base) (/ (- power 1) 2)))
    )
  )
)

(define (fast-exp-iter base power answer)
  (if (= power 0)
    answer
    (if (even power)
      (fast-exp-iter (sq base) (/ power 2) answer)
      (fast-exp-iter base (- power 1) (* answer base))
    )
  )
)

(define (fast-exp base power)
  (fast-exp-iter base power 1))
```

## 1.17
```scheme
(define (remainder dividend divisor)
  (let ((qoutient (floor (/ dividend divisor))))
  (- dividend (* divisor qoutient))))

(define (double x) (+ x x))

(define (halve x) (/ x 2))

(define (even n) (= (remainder n 2) 0))

(define (multiply-successive-doubling a b)
    (if (= b 0)
      0
        (if (even b)
            (multiply-successive-doubling (double a) (halve b))
            (+ a (multiply-successive-doubling a (- b 1)))
        )
    )
)
```

## 1.18
```scheme
;; keep a separate answer so you dont forget what you were meant to calculate in the original question
;; in recursion, state can just be the subproblem and the stack will remember what to do with the result of this subproblem
(define (multiply-successive-doubling-iter a b answer)
  (if (= b 0)
    answer
    (if (even b)
      (multiply-successive-doubling-iter (double a) (halve b) answer)
      (multiply-successive-doubling-iter a (- b 1) (+ a answer)))))
```

## 1.19
```scheme
(define (sq x) (* x x))

(define (fib n)
  (fib-iter 1 0 0 1 n))

(define (fib-iter a b p q count)
  (cond
    ((= count 0) b)
    ((even? count)
      (fib-iter a
        b
        (+ (sq p) (sq q))
        (+ (sq q) (* 2 (* q p)))
        (/ count 2)))
    (else (fib-iter (+ (* b q)
      (* a q)
      (* a p))
      (+ (* b p) (* a q))
      p
      q
      (- count 1)))))
```

## 1.20
```scheme
(define (gcd a b)
    (if (= b 0)
        a
        (gcd b (remainder a b))))

;; 1. applicative order (operands are computed before applying procedures)
;; (gcd 206 40) since 40 != 0, (gcd 40 (remainder 206 40)) and is evaluated to (gcd 40 6)
;; (gcd 40 6) since 6 != 0, (gcd 6 (remainder 40 6)) and is evaluated to (gcd 6 4)
;; (gcd 6 4) since 4 != 0, (gcd 4 (remainder 6 4)) and is evaluated to (gcd 4 2)
;; (gcd 4 2) since 2 != 0, (gcd 2 (remainder 4 2)) and is evaluated to (gcd 2 0)
;; (gcd 2 0) since 0 = 0, 2 is the answer
;; 4 remainder operations are performed

;; 2. normal order (operands are computed when needed)
;; (gcd 206 40) since 40 != 0, (gcd 40 (remainder (206 40)))
;; (gcd 40 (remainder 206 40))
;;    -> is (remainder 206 40) equal to 0?  no, it equals 6 [1 operation]
;;    -> evaluate (gcd (remainder 206 40) (remainder 40 (remainder 206 40)))
;; (gcd (remainder 206 40) (remainder 40 (remainder 206 40)))
;;    -> is (remainder 40 (remainder 206 40)) equal to 0? no, it equals 4 [2 operations]
;;    -> evaluate (gcd (remainder 40 (remainder 206 40)) (remainder (remainder 206 40) (remainder 40 (remainder 206 40))))
;; (gcd (remainder 40 (remainder 206 40)) (remainder (remainder 206 40) (remainder 40 (remainder 206 40))))
;;    -> is (remainder (remainder 206 40) (remainder 40 (remainder 206 40))) equal to 0? no, it equals 2 [4 operations]
;;    -> evaluate (gcd (remainder (remainder 206 40) (remainder 40 (remainder 206 40))) (remainder (remainder 40 (remainder 206 40))) (remainder (remainder 206 40) (remainder 40 (remainder 206 40))))
;; (gcd (remainder (remainder 206 40) (remainder 40 (remainder 206 40))) (remainder (remainder 40 (remainder 206 40))) (remainder (remainder 206 40) (remainder 40 (remainder 206 40))))
;;    -> is (remainder (remainder 40 (remainder 206 40))) (remainder (remainder 206 40) (remainder 40 (remainder 206 40))) equal to 0? yes! [7 operations]
;;    -> return (remainder (remainder 206 40) (remainder 40 (remainder 206 40))) [4 operations]
;; 18 remainder operations are performed
```

## 1.21
```scheme
1 ]=> (smallest-divisor 199)
;Value: 199

1 ]=> (smallest-divisor 1999)
;Value: 1999

1 ]=> (smallest-divisor 19999)
;Value: 7
```

## 1.22
```scheme
(define (search-for-primes start end)
  (timed-prime-test start)
  (if (< start end)
        (search-for-primes (+ 1 start) end))
      -1)

;; bro it takes 0.0000 for everything
;; tried it with much larger numbers

;; 1 ]=> (search-for-primes 1000000000 1000001000)
;; 1000000007 *** 1.0000000000000009e-2
;; 1000000009 *** .01999999999999999
;; 1000000021 *** 9.999999999999995e-3


;; 1 ]=> (search-for-primes 10000000000 10000001000)
;; 10000000019 *** .06
;; 10000000033 *** .06
;; 10000000061 *** 6.0000000000000026e-2

;; 1 ]=> (search-for-primes 100000000000 100000001000)
;; 100000000003 *** .19
;; 100000000019 *** .19
;; 100000000057 *** .20000000000000007

;; the factor is about 6 bw the first two and about 3.4 bw the last two
;; sqrt(10) is about 3.16
;; guess - the remainder operations will take more time with larger values
```

## 1.23
```scheme
(define (next divisor)
  (if (= divisor 2) 3 (+ 2 divisor)))

(define (find-divisor n test-divisor)
  (cond ((> (square test-divisor) n) n)
        ((divides? test-divisor n) test-divisor)
  (else (find-divisor n (next test-divisor)))))

;; bro it did get faster
;; 1 ]=> (search-for-primes 100000000000 100000001000)
;; 100000000003 *** .12
;; 100000000019 *** .13
;; 100000000057 *** .12
;; but not by 2x, my guess still remains the same
;; guess - the remainder operations will take more time with larger values
```

## 1.24
```scheme
(define (sq x) (* x x))

(define (even n) (= 0 (remainder n 2)))

(define (fast-exp-mod base power mod)
  (if (= power 0)
    (remainder 1 mod)
    (if (even power)
      (fast-exp-mod (remainder (sq base) mod) (/ power 2) mod)
      (remainder (* base (fast-exp-mod (sq base) (/ (- power 1) 2) mod)) mod))))



(define (fermat-test n)
  (define (try-it a) (= (fast-exp-mod a n n) a))
  (try-it (+ 1 (random (- n 1)))))

(define (fast-prime? n times)
  (cond
    ((= times 0) true)
    ((fermat-test n) (fast-prime? n (- times 1)))
    (else false)))

(define (timed-prime-test n)
  (start-prime-test n (runtime)))

(define (start-prime-test n start-time)
  ;; (newline)
  ;; (display "start-prime-test ") (display n) (display " ") (display start-time)
  (if (fast-prime? n 100) (report-prime n (- (runtime) start-time))))
    
(define (report-prime n elapsed-time)
  (newline)
  (display n)
  (display " *** ")
  (display elapsed-time))

(define (search-for-primes start end)
  (timed-prime-test start)
  (if (< start end) (search-for-primes (+ 1 start) end))
  end)


(search-for-primes 100000000000 100000001000)

;; 100000000003 *** 0.
;; 100000000019 *** 0.
;; 100000000057 *** 1.0000000000000009e-2

;; factor of optimization = sqrt(n) / 100 * logn = (10 ^ 5.5) / 100 * log (10 ^ 11) = 124

;; todo - Since the Fermat test has Î˜(log n) growth, how would you expect the time to test primes near 1,000,000 to compare with the time needed to test primes near 1000? Do your data bear this out? Can you explain any discrepancy you find?
```

## 1.25
```
no, Alyssa, you will multiply much larger numbers, which will take longer
```

## 1.26
```scheme
(define (expmod base exp m)
    (cond
    ((= exp 0) 1)
    ((even? exp) (remainder (* (expmod base (/ exp 2) m) (expmod base (/ exp 2) m)) m))
  (else (remainder (* base (expmod base (- exp 1) m)) m))))

;; bro you are calling expmod twice bro
;; this will create a recursion tree, with expmod being computed twice for instead of once
```

## 1.27
```scheme
(define (sq x) (* x x))

(define (even n) (= 0 (remainder n 2)))

(define (fast-exp-mod base power mod)
  (if (= power 0)
    (remainder 1 mod)
    (if (even power)
      (fast-exp-mod (remainder (sq base) mod) (/ power 2) mod)
      (remainder (* base (fast-exp-mod base (- power 1) mod)) mod))))

(define (fermat-test n i)
  (let ((themod (fast-exp-mod i n n)))
    ;; (newline )(display "fermat-test ") (display n) (display " ") (display i) (display (= themod i))
    (= themod i)))

(define (test-carmichael-iter n i)
  (if (= i n) #t
  (if (fermat-test n i) (test-carmichael-iter n (+ 1 i))
  #f)))

(define (test-carmichael n) (test-carmichael-iter n 1))

(test-carmichael 561)
(test-carmichael 1105)
(test-carmichael 1729)
(test-carmichael 2465)
(test-carmichael 2821)
(test-carmichael 6601)
```

## 1.28
```scheme
(define (sq x) (* x x))

(define (even n) (= 0 (remainder n 2)))

(define (expmod base power mod)
  (if (= power 0)
    1
    (if (even power)
      (let ((potential-non-trivial-sqrt (expmod base (/ power 2) mod)))
        (let ((the-square (sq potential-non-trivial-sqrt)))
          (if (and
                (not (= -1 potential-non-trivial-sqrt))
                (and
                  (not (= 1 potential-non-trivial-sqrt))
                  (= the-square 1)))
            0
            (remainder the-square mod))))
      (remainder (* base (expmod base (- power 1) mod)) mod))))


(define (miller-rabin-witness? n a)
  (let ((result (expmod a (- n 1) n)))
    (if (= result 0)
      #t
      (not (= result 1)))))

(define (random-till n) (+ 1 (random (- n 1))))

(define (miller-rabin-prime-test n times)
  (if (= times 0)
    #t
    (if (miller-rabin-witness? n (random-till n))
      #f
      (miller-rabin-prime-test n (- times 1)))))

(define (miller-rabin-prime? n)
  (miller-rabin-prime-test n 100))
```

## 1.29
```scheme
(define (even? x) (= (remainder x 2) 0))
(define (sq x) (* x x))
(define (cube x) (* (sq x) x))
(define (incr x) (+ 1 x))

(define (sum term a next b)
  (if (> a b)
    0
    (+
      (term a)
      (sum term (next a) next b))))

(define (integral f a b dx)
  (define (add-dx x) (+ x dx))
  (*
    (sum f (+ a (/ dx 2.0)) add-dx b)
    dx))

(define (integral-simpsons f a b n)
  (let ((h (/ (- b a) n)))
    (define (simpsons-term k)
      (let ((y-k (f (+ a (* k h)))))
        (cond
          ((or (= k n) (= k 0)) y-k)
          ((even? k) (* 2 y-k))
          (else (* 4 y-k)))))
    (*
      (/ h 3.0)
      (sum simpsons-term 0 1+ n))))


(integral-simpsons cube 0 1 100)
(integral-simpsons cube 0 1 1000)
```


## 1.30
```scheme
(define (iterative-sum term a next b)
  (define (iter x result)
    (if (> x b)
      result
      (iter (next x) (+ result (term x)))))
  (iter a 0))
```


## 1.31
```scheme
(define (sq x) (* x x))

(define (product term a next b)
  (if (> a b) 1
    (* (term a) (product term (next a) next b))))

(define (factorial n)
  (product
    (lambda (x) x) 1 1+ n))


  (define (iter x result)
    (if (> x b)
      result
      (iter (next x) (* result (term x)))))
  (iter a 1))

;; wallis product is rewritten as (/ pi 2) = (* (/ 2 1) (/ 2 3) (/ 4 3) (/ 4 5) (/ 6 5) (/ 6 7)..)
;; (* (/ 4 3) (/ 16 15) (/ 36 35))
;; this is used to create the parameter 'term'
(define (pi-product n)
  (product (lambda (x) (/
        (* 4.0 (sq x))
        (- (* 4 (sq x)) 1)))
    1
    1+
    n))


(* 2.0 (pi-product 1000))
```


## 1.32
```scheme
(define (accumulate combiner null-value term a next b)
  (if (> a b) null-value
    (combiner (term a) (accumulate combiner null-value term (next a) next b))))
  
(define (accumulate-iterative combiner null-value term a next b)
  (define (iter i result)
    (if (> i b)
      result
      (iter (next i) (combiner result (term i)))))
  (iter a null-value))
  
(define (sum term a next b)
  (accumulate + 0 term a next b))

(define (product term a next b)
  (accumulate * 1 term a next b))

(sum (lambda (x) x) 1 1+ 100)
(sum (lambda (x) (* x x)) 1 1+ 10)
```


## 1.33
```scheme
(define (filtered-accumulate combiner filter null-value term a next b)
  (if (> a b) null-value
    (if (filter a)
      (combiner
        (term a)
        (filtered-accumulate combiner filter null-value term (next a) next b))
      (filtered-accumulate combiner filter null-value term (next a) next b))))

(define (sum-of-sqd-primes a b)
  (filtered-accumulate
    +
    prime?
    0
    (lambda (x) (* x x))
    a
    1+
    b))

(define (product-relative-primes n)
  (filtered-accumulate
    *
    (lambda (x) (= (gcd x n) 1))
    1
    (lambda (x) x)
    1
    1+
    n))
```

## 1.34
```scheme
(define (f g) (g 2))

;; (f f) -> (f 2) -> (2 2)
;; this will throw an error, since the operation is now '2', and it cannot be applied
```

## 1.35
```scheme
;; x = 1 + 1/x, if x is the fixed-point
;; x^2 = x + 1
;; the roots of this eqn are phi and psi
(define (golden-ratio)
    (fixed-point (lambda (y) (+ 1 (/ 1 y)))
    1.0))

(golden-ratio)
```


## 1.36
```scheme
(define (fixed-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v2 v1)) tolerance))
  (define (try guess)
    (let ((next (f guess)))
      (newline) (display "try: guess = ") (display guess) (display ", next = ") (display next)
      (if (close-enough? guess next)
        guess
        (try next))))
  (try first-guess))

;; without average damp
;; try: guess = 10., next = 2.9999999999999996
;; try: guess = 2.9999999999999996, next = 6.2877098228681545
;; try: guess = 6.2877098228681545, next = 3.7570797902002955
;; try: guess = 3.7570797902002955, next = 5.218748919675316
;; try: guess = 5.218748919675316, next = 4.1807977460633134
;; try: guess = 4.1807977460633134, next = 4.828902657081293
;; try: guess = 4.828902657081293, next = 4.386936895811029
;; try: guess = 4.386936895811029, next = 4.671722808746095
;; try: guess = 4.671722808746095, next = 4.481109436117821
;; try: guess = 4.481109436117821, next = 4.605567315585735
;; try: guess = 4.605567315585735, next = 4.522955348093164
;; try: guess = 4.522955348093164, next = 4.577201597629606
;; try: guess = 4.577201597629606, next = 4.541325786357399
;; try: guess = 4.541325786357399, next = 4.564940905198754
;; try: guess = 4.564940905198754, next = 4.549347961475409
;; try: guess = 4.549347961475409, next = 4.5596228442307565
;; try: guess = 4.5596228442307565, next = 4.552843114094703
;; try: guess = 4.552843114094703, next = 4.55731263660315
;; try: guess = 4.55731263660315, next = 4.554364381825887
;; try: guess = 4.554364381825887, next = 4.556308401465587
;; try: guess = 4.556308401465587, next = 4.555026226620339
;; try: guess = 4.555026226620339, next = 4.55587174038325

;; with average damp
;; try: guess = 10., next = 6.5
;; try: guess = 6.5, next = 5.095215099176933
;; try: guess = 5.095215099176933, next = 4.668760681281611
;; try: guess = 4.668760681281611, next = 4.57585730576714
;; try: guess = 4.57585730576714, next = 4.559030116711325
;; try: guess = 4.559030116711325, next = 4.55613168520593
;; try: guess = 4.55613168520593, next = 4.555637206157649
```

## 1.37
```scheme
(define (cont-frac n d k)
  (define (cont-frac-sub i)
    (if (> i k)
      0
      (/
        (n i)
        (+ (d i) (cont-frac-sub (+ i 1))))))
  (cont-frac-sub 1))

(define (golden-ratio k)
  (/
    1
    (cont-frac
      (lambda (x) 1.0)
      (lambda (x) 1.0)
      k)))

(golden-ratio 12)

;; Value: 1.6180555555555558


(define (cont-frac-iterative n d k)
  (define (cont-frac-iter i ans)
    (if (< i 1)
      ans
      (cont-frac-iter
          (- i 1)
          (/ (n i) (+ (d i) ans)))))
  (cont-frac-iter k 0))
```

## 1.38
```scheme
(define (e-minus-2 k)
  (cont-frac
    (lambda (x) 1.0)
    (lambda (x)
        (if (= 2 (remainder x 3))
            (* (/ 2.0 3) (+ x 1))
            1.0))
    k))

(define (e-approximation k)
  (+ 2 (e-minus-2 k)))

(e-approximation 20)
;Value: 2.718281828459045
```


## 1.39
```scheme
(define (tan-cf x k)
  (/
    (cont-frac
      (lambda (i) (* -1 (* x x)))
      (lambda (i) (- (* 2 i) 1))
      k)
    (* -1 x)))


(define pi 3.14159265359)

(tan-cf (/ pi 4) 10)
;Value: 1.0000000000001035
```

## 1.40
```scheme
(define (fixed-point-of-transform f transform guess)
  (fixed-point (transform f) guess))

(define (derivative f)
  (define dx 0.001)
  (lambda (x) (/ (- (f (+ x dx)) (f x)) dx)))

(define (newtons-transform f)
  (lambda (x)(- x (/ (f x) ((derivative f) x)))))

(define (newtons-method f first-guess)
  (fixed-point-of-transform f newtons-transform first-guess))

(define (cubic a b c)
  (lambda (x) (+ (cube x) (+ (* a (square x)) (+ (* b x) c)))))

(newtons-method (cubic -6 11 -6) 1.5)
```

## 1.41
```scheme
(define (double f)
  (lambda (x) (f (f x))))

(((double (double double)) 1+) 5)
;Value: 21
```


## 1.42
```scheme
(define (compose f g)
  (lambda (x) (f (g x))))

((compose square 1+) 6)
;Value: 49
```