# chapter 3 exercises

## 3.1
```scheme
(define (accumulator val)
  (lambda (augend)
    (begin (set! val (+ val augend)) val)))
```


## 3.2
```scheme
(define (make-monitored f)
  (define counter 0)
  (lambda (prompt)
    (cond
      ((equal? prompt 'how-many-calls?) counter)
      ((equal? prompt 'reset-counter) (set! counter 0))
      (else
        (begin
          (set! counter (+ 1 counter))
          (f prompt))))))
```


## 3.3
```scheme
(define (make-account balance password)
  (define (withdraw amount)
    (if (>= balance amount)
      (begin (set! balance (- balance amount)) balance)
      "Insufficient funds"))

  (define (deposit amount)
    (set! balance (+ balance amount))
    balance)

  (define (deny amount) "Incorrect password")
    
  (define (dispatch p m)
    (if (eq? password p)
      (cond
        ((eq? m 'withdraw) withdraw)
        ((eq? m 'deposit) deposit)
        (else (error "Unknown request: MAKE-ACCOUNT" m)))
      deny))

  dispatch)
```


## 3.4
```scheme
(define call-the-cops
  "Any gangster will tell you that the strain and anxiety of busting a safe are greatly diminished if you've a look-out man ready at any moment to say 'Cheese it, the cops!'")

(define (deny amount)
    (if (>= failed-attempts 7)
      call-the-cops
      (begin
        (newline) (display "amount invoked, failed attempts = ") (display failed-attempts)
        (set! failed-attempts (+ 1 failed-attempts))
        "Incorrect password")))
  
(define (dispatch p m)
  (if (eq? password p)
    (begin
      (set! failed-attempts 0)
      (cond
        ((eq? m 'withdraw) withdraw)
        ((eq? m 'deposit) deposit)
        (else (error "Unknown request: MAKE-ACCOUNT" m))))
    deny))
```


## 3.5
```scheme
(define (random-in-range low high)
  (let ((range (- high low)))
    (lambda () (+ low (random range)))))

(define (unit-circle-predicate x-generator y-generator)
  (let ((x (x-generator)) (y (y-generator)))
    (<
      (+ (square (- x 1.0)) (square (- y 1.0)))
      (square 1.0))))
  
(define (estimate-integral predicate x1 x2 y1 y2 trials)
  (define rect-area (* (- x2 x1) (- y2 y1)))
  (define (integral-experiment)
    (predicate (random-in-range x1 x2) (random-in-range y1 y2)))
  (* rect-area (monte-carlo trials integral-experiment)))


(define (estimate-pi trials)
  (estimate-integral
    unit-circle-predicate
    0.0 2.0 0.0 2.0
    trials))

(define (monte-carlo trials experiment)
  (define (iter trials-remaining trials-passed)
    (cond ((= trials-remaining 0) (/ trials-passed trials))
          ((experiment) (iter (- trials-remaining 1) (+ trials-passed 1)))
          (else (iter (- trials-remaining 1) trials-passed))))
    (iter trials 0))

(estimate-pi 100000)

```